#Options
GenerateScanner = true
GenerateParser = true
Language = Java
ScannerName = Lexico
ParserName = Sintatico
SemanticName = Semantico
ScannerCaseSensitive = true
ScannerTable = Compact
Input = Stream
Parser = LL
#RegularDefinitions
prefixo: (i|f|b|s)_
#Tokens
// equipe: Cristian Monster
		// Lucas de Farias Teixeira
		// Vinícius Gabriel de Souza


// lê e ignora caracteres de formatação
:[\s\t\n]

// símbolos especiais
"&&"
"||"
"!"
"=="
"!="
"<"
">"
"+"
"-"
"*"
"/"
","
";"
"="
"("
")"

palavra: [a-z] [a-zA-Z]*

// palavras reservadas

main = palavra: "main"
end = palavra: "end"
if = palavra: "if"
elif = palavra: "elif"
else = palavra: "else"
false = palavra: "false"
true = palavra: "true"
read = palavra: "read"
write = palavra: "write"
writeln = palavra: "writeln"
repeat = palavra: "repeat"
until = palavra: "until"
while = palavra: "while"


identificador:! {prefixo}([a-z][A-Z]?|[A-Z])([a-z0-9][A-Z]?)*

int: [0-9]|[1-9][0-9]*

float: ([0-9]|[1-9][0-9]*),([0-9]|[0-9]*[1-9])

string: \"([^%\n\"]|%x)*\"

// comentário de bloco
:! >@\n[^@]*\n@<
#NonTerminals
<main>

<lista_instrucoes>
<opc_lista_instrucoes>
<instrucao>
<lista_comando>
<comando>
<lista_id>
<atribuicao>
<entrada>
<saida>
<selecao>
<repeticao>
<lista_entrada>
<opcional_string>
<lista_exp>
<expressao>
<opcional_elif>
<elif>
<opcional_else>
<repeat_comando>

<fator>
<termo1>
<termo>
<aritmetica1>
<aritmetica>
<operador_relacional>
<relacional1>
<relacional>
<elemento>
<expressao1>

<opc_instrucao>
<opc_atribuicao>
<opc_lista_id>
<opc_lista_comando>
<opc_lista_entrada>
<opc_lista_exp>
#Grammar
<main> ::= main <lista_instrucoes> end ;

<lista_instrucoes> ::= <instrucao> <opc_lista_instrucoes>;
<opc_lista_instrucoes> ::= <lista_instrucoes> | î;

<instrucao> ::= <opc_instrucao> ";";
<opc_instrucao> ::= <lista_id>  <opc_atribuicao> | <entrada> | <saida> | <selecao> | <repeticao> ; 
<opc_atribuicao> ::= "=" <expressao> | î;

<lista_id> ::= identificador <opc_lista_id>;
<opc_lista_id> ::=  "," <lista_id> | î ;

<lista_comando> ::= <comando> ";" <opc_lista_comando>;
<opc_lista_comando> ::= <lista_comando> | î ;
<comando> ::=  <atribuicao> | <entrada> | <saida> | <selecao> | <repeticao> ; 

<atribuicao> ::= <lista_id> "=" <expressao> ;

<entrada> ::= read "(" <lista_entrada> ")" ;
<lista_entrada> ::=  <opcional_string> identificador <opc_lista_entrada>;
<opc_lista_entrada> ::=  "," <lista_entrada> | î  ;
<opcional_string> ::= string "," | î ;

<saida> ::= write "(" <lista_exp> ")" | writeln "(" <lista_exp> ")" ;
<lista_exp> ::= <expressao> <opc_lista_exp>;
<opc_lista_exp> ::= "," <lista_exp> | î ;

<selecao> ::= if <expressao> <lista_comando> <opcional_elif> <opcional_else> end ;
<opcional_elif> ::= <elif> <opcional_elif> | î ;
<elif> ::= elif <expressao> <lista_comando> ;
<opcional_else> ::= else <lista_comando> | î ;

<repeticao> ::= repeat <lista_comando> <repeat_comando> <expressao> ;
<repeat_comando> ::= while | until ;

<expressao> ::= <elemento> <expressao1> ; 
<expressao1> ::= î | "&&" <elemento> <expressao1> | "||" <elemento> <expressao1> ; 
<elemento> ::= <relacional> | true | false | "!" <elemento> ;
<relacional> ::= <aritmetica> <relacional1> ;
<relacional1> ::= î | <operador_relacional> <aritmetica> ;
<operador_relacional>::= "==" | "!=" | "<" | ">" ;
<aritmetica> ::= <termo> <aritmetica1> ;
<aritmetica1> ::= î | "+" <termo> <aritmetica1> | "-" <termo> <aritmetica1> ;
<termo> ::= <fator> <termo1> ;
<termo1> ::= î | "*" <fator> <termo1> | "/" <fator> <termo1> ;
<fator> ::= identificador | int | float | string | "(" <expressao> ")" | "+" <fator> | "-" <fator> ; 

